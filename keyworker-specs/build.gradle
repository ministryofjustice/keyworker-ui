plugins {
    id "groovy"
    id 'com.github.ben-manes.versions' version '0.27.0'
}

ext {

    ext {
        drivers = [
//                'chrome',
                'chromeHeadless'
        ]
        groovyVersion = '2.5.9'
        gebVersion = '3.3'
        seleniumVersion = '3.141.59'
        chromeDriverVersion = '3.141.59'
    }
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

dependencyUpdates {
    rejectVersionIf {
        isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
    }
}

repositories {
    mavenCentral()
}

project.sourceCompatibility = 11
project.targetCompatibility = 11

dependencies {
    testCompile('org.spockframework:spock-core:1.3-groovy-2.5') {
        exclude group: "org.codehaus.groovy"
    }

    testCompile "org.codehaus.groovy:groovy-all:$groovyVersion"
    testCompile "org.gebish:geb-core:$gebVersion"
    testCompile "org.gebish:geb-spock:$gebVersion"
    testCompile "org.seleniumhq.selenium:selenium-support:$seleniumVersion"
    testCompile "org.seleniumhq.selenium:selenium-chrome-driver:$seleniumVersion"
    testCompile "org.seleniumhq.selenium:selenium-firefox-driver:$seleniumVersion"
    testCompile "io.github.http-builder-ng:http-builder-ng-apache:1.0.4"

    testCompile 'com.github.tomakehurst:wiremock-standalone:2.26.0'
    testCompile 'com.github.tomjankes:wiremock-groovy:0.2.0'
    testCompile 'org.slf4j:slf4j-api:1.7.30'
    testCompile 'ch.qos.logback:logback-classic:1.2.3'
    testCompile 'com.auth0:java-jwt:3.9.0'
}

drivers.each { driver ->
    task "${driver}Test"(type: Test) {
        group JavaBasePlugin.VERIFICATION_GROUP

        outputs.upToDateWhen { false } // Always run tests

        systemProperty 'geb.build.reportsDir', reporting.file("geb/${name}")
        systemProperty 'geb.env', driver
    }
}

test {
    dependsOn drivers.collect { tasks["${it}Test"] }
    enabled = false
}

